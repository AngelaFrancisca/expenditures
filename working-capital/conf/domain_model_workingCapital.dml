package module.workingCapital.domain;

enum WorkingCapitalInitialization.WorkingCapitalInitializationState as WorkingCapitalInitializationState;


valueType myorg.domain.util.Money as Money {
    externalizeWith {
        String exportAsString();
    }

    internalizeWith importFromString();
}


class WorkingCapitalSystem {
}

class WorkingCapitalYear {
	Integer year;
}

class WorkingCapital {
}

class WorkingCapitalInitialization {
	DateTime requestCreation;
	Money requestedAnualValue;
	Money authorizedAnualValue;
	Money maxAuthorizedAnualValue;
	String fiscalId;
	String bankAccountId;
	DateTime aprovalByUnitResponsible;
	DateTime authorizationByUnitResponsible;
	DateTime verificationByAccounting;
	WorkingCapitalInitializationState state;
}

class WorkingCapitalProcess extends .module.workflow.domain.WorkflowProcess {
}

class AcquisitionClassification {
	String description;
	String economicClassification;
	String pocCode;
}


relation MyOrgWorkingCapitalSystem {
	.myorg.domain.MyOrg playsRole myOrg;
	WorkingCapitalSystem playsRole workingCapitalSystem;
}

relation WorkingCapitalSystemWorkingCapitalYear {
	WorkingCapitalSystem playsRole workingCapitalSystem;
	WorkingCapitalYear playsRole workingCapitalYears {
		multiplicity *;
	}
}

relation WorkingCapitalSystemWorkingCapital {
	WorkingCapitalSystem playsRole workingCapitalSystem;
	WorkingCapital playsRole workingCapitals {
		multiplicity *;
	}
}

relation WorkingCapitalSystemWorkingCapitalInitialization {
	WorkingCapitalSystem playsRole workingCapitalSystem;
	WorkingCapitalInitialization playsRole workingCapitalInitializations {
		multiplicity *;
	}
}

relation WorkingCapitalWorkingCapitalYear {
	WorkingCapitalYear playsRole workingCapitalYear;
	WorkingCapital playsRole workingCapitals {
		multiplicity *;
	}
}

relation WorkingCapitalProcessWorkingCapital {
	WorkingCapitalProcess playsRole workingCapitalProcess;
	WorkingCapital playsRole workingCapital;
}

relation WorkingCapitalUnit {
	.pt.ist.expenditureTrackingSystem.domain.organization.Unit playsRole unit;
	WorkingCapital playsRole workingCapitals {
		multiplicity *;
	}
}

relation WorkingCapitalMovementResponsible {
	.module.organization.domain.Person playsRole movementResponsible;
	WorkingCapital playsRole movementResponsibleWorkingCapitals {
		multiplicity *;
	}
}

relation WorkingCapitalWorkingCapitalInitialization {
	WorkingCapital playsRole workingCapital;
	WorkingCapitalInitialization playsRole workingCapitalInitializations {
		multiplicity *;
	}
}

relation WorkingCapitalInitializationRequestor {
	.module.organization.domain.Person playsRole requestor;
	WorkingCapitalInitialization playsRole requestedWorkingCapitalInitializations {
		multiplicity *;
	}
}

relation WorkingCapitalInitializationResponsibleForUnitApproval {
	.pt.ist.expenditureTrackingSystem.domain.authorizations.Authorization playsRole responsibleForUnitApproval;
	WorkingCapitalInitialization playsRole approvedWorkingCapitalInitializations {
		multiplicity *;
	}
}

relation WorkingCapitalInitializationResponsibleForUnitAuthorization {
	.module.organization.domain.Accountability playsRole responsibleForUnitAuthorization;
	WorkingCapitalInitialization playsRole authorizedWorkingCapitalInitializations {
		multiplicity *;
	}
}

relation WorkingCapitalInitializationResponsibleForAccountingVerification {
	.module.organization.domain.Accountability playsRole responsibleForAccountingVerification;
	WorkingCapitalInitialization playsRole verifiedAccountingWorkingCapitalInitializations {
		multiplicity *;
	}
}

relation WorkingCapitalSystemAccountingUnit {
	WorkingCapitalSystem playsRole workingCapitalSystemFromAccountingUnit;
	.module.organization.domain.Unit playsRole accountingUnit;
}

relation WorkingCapitalSystemManagementUnit {
	WorkingCapitalSystem playsRole workingCapitalSystemFromManagmentUnit;
	.module.organization.domain.Unit playsRole managementUnit;
}

relation WorkingCapitalSystemAccountingAccountabilityType {
	WorkingCapitalSystem playsRole workingCapitalSystemFromAccountingAccountabilityType;
	.module.organization.domain.AccountabilityType playsRole accountingAccountabilityType;
}

relation WorkingCapitalSystemManagementAccountabilityType {
	WorkingCapitalSystem playsRole workingCapitalSystemFromManagementeAccountabilityType;
	.module.organization.domain.AccountabilityType playsRole managementAccountabilityType;
}

relation WorkingCapitalSystemAcquisitionClassification {
	WorkingCapitalSystem playsRole workingCapitalSystem;
	AcquisitionClassification playsRole acquisitionClassifications {
		multiplicity *;
	}
}
